# Distributed under the terms of the GNU General Public License v2

EAPI=6
inherit autotools eutils fcaps flag-o-matic

DESCRIPTION="My TraceRoute, an Excellent network diagnostic tool"
HOMEPAGE="http://www.bitwizard.nl/mtr/"
SRC_URI="{{src_uri}}"

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="*"
IUSE="gtk ipv6 ncurses"

S="${WORKDIR}/{{github_user}}-{{github_repo}}-{{sha[:7]}}"

RDEPEND="
        gtk? (
                dev-libs/glib:2
                x11-libs/gtk+:2
        )
        ncurses? ( sys-libs/ncurses:0= )
"
DEPEND="
        ${RDEPEND}
        sys-devel/autoconf
        virtual/pkgconfig
"

DOCS=( AUTHORS FORMATS NEWS README.md SECURITY TODO )
FILECAPS=( cap_net_raw usr/sbin/mtr-packet )

src_prepare() {
        default

        eautoreconf
}

src_configure() {
        # In the source's configure script -lresolv is commented out. Apparently it
        # is needed for 64bit macos still.
        [[ ${CHOST} == *-darwin* ]] && append-libs -lresolv
        econf \
                $(use_enable ipv6) \
                $(use_with gtk) \
                $(use_with ncurses)
}

src_test() {
        [[ "$UID" = 0 ]] && default
}

pkg_postinst() {
        fcaps_pkg_postinst

        if use prefix && [[ ${CHOST} == *-darwin* ]] ; then
                ewarn "mtr needs root privileges to run.  To grant them:"
                ewarn " % sudo chown root ${EPREFIX}/usr/sbin/mtr"
                ewarn " % sudo chmod u+s ${EPREFIX}/usr/sbin/mtr"
        fi
}